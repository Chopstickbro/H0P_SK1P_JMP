module multi_dec_ctr #(
    DIGITS = 4 : DIGITS >= 2  // number of digits
  ) (
    input clk,                // clock
    input rst,                // reset
    input inc,                // increment counter
    output digits[DIGITS][4]  // digit values
  ) {
 
  .clk(clk) {
    edge_detector edge_detector(#RISE(1), #FALL(0));
    .rst(rst) {
    decimal_counter dctr [DIGITS]; // digit counters
    }
  }
 
  always {
    edge_detector.in = inc;
    dctr.inc[0] = edge_detector.out;   // increment the first digit
    digits = dctr.value; // output the values
 
    // if the previous digit overflows, increment the next
    dctr.inc[1+:DIGITS-1] = dctr.ovf[0+:DIGITS-1];
  }
}